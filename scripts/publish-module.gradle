apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar

                pom {
                    name = 'Related Digital Visilabs Android SDK'
                    description = 'Related Digital Visilabs Android SDK'
                    url = 'https://github.com/relateddigital/visilabs-android'
                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'relateddigital'
                            name = 'Related Digital'
                            email = 'developer@relateddigital.com'
                        }
                    }
                    scm {
                        connection = 'scm:git@github.com/relateddigital/visilabs-android.git'
                        developerConnection = 'scm:git@github.com/relateddigital/visilabs-android.git'
                        url = 'https://github.com/relateddigital/visilabs-android'
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications
}